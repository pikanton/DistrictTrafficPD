USE ГАИ2
DROP TABLE Регистрация_Лог
GO
CREATE TABLE Регистрация_Лог(
  Код INT IDENTITY PRIMARY KEY
  , Тип VARCHAR(10) CHECK (Тип IN ('Добавление', 'Удаление'))
  , [Время операции] DATETIME
  , Компьютер VARCHAR(255)
  , [Код регистрации] INT
  , [Регистрационный знак] VARCHAR(8)
  , Дата DATETIME
  , [Код автомобиля] INT
  , [Код водителя] INT
  , [Код сотрудника] INT
)
GO
DROP VIEW Регистрация_Лог_Представление
GO
CREATE VIEW Регистрация_Лог_Представление
AS
  SELECT Код,
    Тип,
    [Время операции],
    Компьютер,
    [Регистрационный знак],
	Дата,
	dbo.ФИОВодителя([Код водителя]) AS Водитель,
	dbo.ФИОСотрудника([Код сотрудника]) AS Сотрудник,
	dbo.МаркаМодельVINАвтомобиля([Код автомобиля]) AS Автомобиль
    FROM Регистрация_Лог
GO
DROP TRIGGER trgРегистрацияLog
GO
CREATE TRIGGER trgРегистрацияLog ON Регистрация
  AFTER INSERT, UPDATE, DELETE
AS
  DECLARE @dateLog DATETIME
  SET @dateLog = GETDATE()
  INSERT INTO Регистрация_Лог(Тип, [Время операции], Компьютер, [Код регистрации],
              [Регистрационный знак], Дата, [Код автомобиля], [Код водителя], [Код сотрудника])
    SELECT 'Удаление', @dateLog, SYSTEM_USER, d.Код, d.[Регистрационный знак],
           d.Дата, d.[Код автомобиля], d.[Код водителя], d.[Код сотрудника]
      FROM deleted d
  INSERT INTO Регистрация_Лог(Тип, [Время операции], Компьютер, [Код регистрации],
              [Регистрационный знак], Дата, [Код автомобиля], [Код водителя], [Код сотрудника])
    SELECT 'Добавление', @dateLog, SYSTEM_USER, i.Код, i.[Регистрационный знак],
           i.Дата, i.[Код автомобиля], i.[Код водителя], i.[Код сотрудника]
      FROM inserted i
GO
DROP PROC Откат_Регистрации
GO
CREATE PROC Откат_Регистрации @lastId INT
AS
  DECLARE logCursor CURSOR FOR
    SELECT Тип, [Время операции], Компьютер, [Код регистрации],
           [Регистрационный знак], Дата, [Код автомобиля], [Код водителя], [Код сотрудника]
      FROM Регистрация_Лог WHERE Код >= @lastId 
	  ORDER BY Код DESC
  DECLARE @Тип VARCHAR(10), @ВремяОперции DATETIME, @Компьютер VARCHAR(255), @КодРегистрации INT, @РегистрационныйЗнак VARCHAR(8),
          @Дата DATETIME, @КодАвтомобиля INT, @КодВодителя INT, @КодСотрудника INT
  SET IDENTITY_INSERT Регистрация ON
  OPEN logCursor
  FETCH logCursor INTO @Тип, @ВремяОперции, @Компьютер, @КодРегистрации, @РегистрационныйЗнак, @Дата,
                       @КодАвтомобиля, @КодВодителя, @КодСотрудника
  WHILE @@FETCH_STATUS = 0
  BEGIN
    IF @Тип = 'Добавление'
      DELETE FROM Регистрация WHERE Код = @КодРегистрации
    ELSE
      INSERT INTO Регистрация (Код,[Регистрационный знак], Дата, [Код автомобиля], [Код водителя], [Код сотрудника])
	    VALUES (@КодРегистрации, @РегистрационныйЗнак, @Дата, @КодАвтомобиля, @КодВодителя, @КодСотрудника)
    FETCH logCursor INTO @Тип, @ВремяОперции, @Компьютер, @КодРегистрации, @РегистрационныйЗнак, @Дата,
                       @КодАвтомобиля, @КодВодителя, @КодСотрудника
  END
  SET IDENTITY_INSERT Регистрация OFF
  CLOSE logCursor
  DEALLOCATE logCursor
  DELETE FROM Регистрация_Лог WHERE Код >= @lastId
GO
/*
SELECT * FROM Регистрация_Лог
SELECT * FROM Регистрация


DELETE FROM Регистрация
INSERT INTO Регистрация VALUES
  ('2819KX-3','20221219',1,1,1)
  ,('1566KK-3','20221220',1,2,3)
  ,('6593KB-3','20221221',2,2,5)
  ,('2417II-3','20220504',3,3,4)
  ,('6767KI-3','20230909',1,3,1)
  ,('1212KK-3','20231010',3,2,3)
  ,('5643KB-3','20231212',2,3,4)

SELECT * FROM Регистрация_Лог
SELECT * FROM Регистрация

BEGIN TRAN
EXEC Откат_Регистрации 3
SELECT * FROM Регистрация_Лог
SELECT * FROM Регистрация
ROLLBACK TRAN
*/